varnishtest "Test .dump() for files"

server s1 {
   rxreq
   txresp
} -repeat 1 -start

shell {
    cat > "${tmp}/test.ini" <<'EOF'
Var: what a hell?
Variable: hell
field1:
field2: ""
field3: Hello "\o/"! รก
EOF
}

varnish v1 -vcl+backend {
    import ${vmod_cfg};

    sub vcl_init {
        new file = cfg.file(
            "file://${tmp}/test.ini",
            period=0,
            format=ini,
            name_delimiter=":",
            value_delimiter=";");
    }

    sub vcl_recv {
        if (req.http.Synth) {
            return (synth(200, "OK"));
        }
    }

    sub vcl_deliver {
        set resp.http.result = file.dump(prefix=req.http.prefix);
    }

    sub vcl_synth {
        set resp.http.Content-Type = "application/json";
        file.dump(stream=true);
        return (deliver);
    }
} -start

client c1 {
    txreq
    rxresp
    expect resp.http.result == {{"Var":"what a hell?","Variable":"hell","field1":"","field2":"\"\"","field3":"Hello \"\\o/\"! \u00c3\u00a1"}}

    txreq -hdr "prefix: Vari"
    rxresp
    expect resp.http.result == {{"Variable":"hell"}}

    txreq -hdr "Synth: 1"
    rxresp
    expect resp.body == {{"Var":"what a hell?","Variable":"hell","field1":"","field2":"\"\"","field3":"Hello \"\\o/\"! \u00c3\u00a1"}}
} -run

varnish v1 -expect client_req == 3

varnish v1 -expect MGT.child_panic == 0

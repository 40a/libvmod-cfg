varnishtest "Test .is_set() for ini files"

server s1 {
   rxreq
   txresp
} -repeat 1 -start

shell {
    cat > "${tmp}/test.ini" <<'EOF'
field1: value1

[section1]
field1: value1
field2:
    value2.1
    value2.2
    value2.3
EOF
}

varnish v1 -vcl+backend {
    import cfg from "${vmod_topbuild}/src/.libs/libvmod_cfg.so";

    sub vcl_init {
        new file = cfg.file(
            "file://${tmp}/test.ini",
            period=0,
            format=ini,
            name_delimiter=":",
            value_delimiter=";");

        if (!file.is_set("field1")) {
            return (fail);
        }
    }

    sub vcl_deliver {
        set resp.http.result1 = file.is_set("FIELD1");
        set resp.http.result2 = file.is_set("field1");
        set resp.http.result3 = file.is_set("SECTION1:field1");
        set resp.http.result4 = file.is_set("section1:field1");
        set resp.http.result5 = file.is_set("section1:field2");
        set resp.http.dump = file.dump();
    }
} -start

client c1 {
    txreq
    rxresp
    expect resp.http.result1 == "false"
    expect resp.http.result2 == "true"
    expect resp.http.result3 == "false"
    expect resp.http.result4 == "true"
    expect resp.http.result5 == "true"
    expect resp.http.dump == {{"field1":"value1","section1:field1":"value1","section1:field2":"value2.1;value2.2;value2.3"}}
} -run

varnish v1 -expect client_req == 1

varnish v1 -expect MGT.child_panic == 0

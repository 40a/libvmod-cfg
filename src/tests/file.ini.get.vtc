varnishtest "Test .get() for ini files"

server s1 {
   rxreq
   txresp
} -repeat 1 -start

shell {
    cat > "${tmp}/test.ini" <<'EOF'
field1: value1

[section1]
field1: value1
field2:
    value2.1
    value2.2
    value2.3
EOF
}

varnish v1 -vcl+backend {
    import ${vmod_cfg};

    sub vcl_init {
        new file = cfg.file(
            "file://${tmp}/test.ini",
            period=0,
            format=ini,
            name_delimiter=":",
            value_delimiter=";");

        if (file.get("field1") != "value1") {
            return (fail);
        }
    }

    sub vcl_deliver {
        set resp.http.result1 = file.get("FIELD1", "-");
        set resp.http.result2 = file.get("field1", "-");
        set resp.http.result3 = file.get("SECTION1:field1", "-");
        set resp.http.result4 = file.get("section1:field1", "-");
        set resp.http.result5 = file.get("section1:field2", "-");
        set resp.http.dump = file.dump();
    }
} -start

client c1 {
    txreq
    rxresp
    expect resp.http.result1 == "-"
    expect resp.http.result2 == "value1"
    expect resp.http.result3 == "-"
    expect resp.http.result4 == "value1"
    expect resp.http.result5 == "value2.1;value2.2;value2.3"
    expect resp.http.dump == {{"field1":"value1","section1:field1":"value1","section1:field2":"value2.1;value2.2;value2.3"}}
} -run

varnish v1 -expect client_req == 1

varnish v1 -expect MGT.child_panic == 0

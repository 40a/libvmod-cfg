varnishtest "Test periodical reloads of files"

server s_origin1 {
    rxreq
    txresp
} -repeat 1 -start

server s_origin2 {
    rxreq
    expect req.url == "/test.ini"
    txresp -body "field: foo"

    rxreq
    expect req.url == "/test.ini"
    txresp -body "field: bar"
} -repeat 1 -start

varnish v_origin2 -vcl {
    backend default {
        .host = "${s_origin2_addr}";
        .port = "${s_origin2_port}";
    }

    sub vcl_recv {
        return (pass);
    }
} -start

varnish v1 -vcl {
    import ${vmod_cfg};

    backend default {
        .host = "${s_origin1_addr}";
        .port = "${s_origin1_port}";
    }

    sub vcl_init {
        new file = cfg.file(
            "http://${v_origin2_addr}:${v_origin2_port}/test.ini",
            period=2,
            curl_connection_timeout=2000,
            curl_transfer_timeout=2000,
            curl_ssl_verify_peer=false,
            curl_ssl_verify_host=false,
            curl_ssl_cafile="",
            curl_ssl_capath="",
            curl_proxy="",
            format=ini,
            name_delimiter=":",
            value_delimiter=";");
    }

    sub vcl_deliver {
        set resp.http.result = file.get("field", "-");
        set resp.http.dump = file.dump();
    }
} -start

client c1 {
    txreq
    rxresp
    expect resp.http.result == "foo"
    expect resp.http.dump == {{"field":"foo"}}

    delay 3.0

    txreq
    rxresp
    expect resp.http.result == "bar"
    expect resp.http.dump == {{"field":"bar"}}
} -run

varnish v1 -expect client_req == 2

varnish v1 -expect MGT.child_panic == 0
